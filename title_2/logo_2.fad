
* ---
* ---	FADE
* ---
.local	FADE

	stx	jsr+1
	sty	jsr+2

	ldx	#6
	jsr	wait

	mva	#16	cnt

lcol	mva	<tcol	fcnt
hcol	mva	>tcol	fcnt+1

	ldx	#2
	jsr	wait

jsr	jsr	$ffff

	dec	cnt
	bne	lcol

	ldx	#6

wait	lda:cmp:req cloc
	dex
	bne	wait
	rts

cnt	brk
.end


* ---
* ---	FADE IN
* ---
.local	FADE_IN

	ldx	<fade
	ldy	>fade
	jmp	:fade

fade	ldy	#0
	mva	(fcnt),y	fadr

	iny
	lda	(fcnt),y	
	sne:rts

	sta	fadr+1

	lda	(fadr),y
	tax

	iny
	eor	(fcnt),y
	beq	skp

	dey

	pha

	and	#$f0
	beq	plus1

plus16	txa
	add	#$10
	tax

plus1	pla
	and	#$0f
	seq
	inx

	txa
	sta	(fadr),y

skp	adw	fcnt	#3

	jmp	fade
.end


* ---
* ---	FADE OUT
* ---
.local	FADE_OUT

	ldx	<fade
	ldy	>fade
	jmp	:fade

fade	ldy	#0
	mva	(fcnt),y	fadr

	iny
	lda	(fcnt),y
	sne:rts

	sta	fadr+1

	lda	(fadr),y
	tax

	and	#$0f
	beq	skp0

	dex
skp0
	txa
	and	#$f0
	beq	skp1

	txa
	sub	#$10
	tax
skp1
	txa

	sta	(fadr),y

	adw	fcnt	#3

	jmp	fade
.end


* ---
* ---	SAVE COLOR
* ---
.local	SAVE_COLOR

	mva	fade.lcol+1	fcnt
	mva	fade.hcol+1	fcnt+1

loop	ldy	#0
	mva	(fcnt),y	fadr

	iny
	lda	(fcnt),y
	sne:rts

	sta	fadr+1

	lda	(fadr),y

	iny
	sta	(fcnt),y

	dey
	mva	#0	(fadr),y	; black screen (all colors = $00)

	adw	fcnt	#3

	jmp	loop
.end


tcol	dta t(c1,c2,c3,c4,c6,c8,p0,p1,p2,p3)
	dta t(c11,c12,c13,c14,c15,c16,c17,c18,c19)
	dta t(c20,c21,c22,c23,c24,c25,c26,c28,c29)
	dta t(c30,c31,c32,c33,c34,c35,c36,c37)
	dta t(c42,c43,c44,c45,c46,c47,c49,c51,c53)
	dta t(c56)
	dta t(c63,c64,c68)
	dta t(c91,c93,c94)
	dta t(c105,c106,c109)
	dta t(c113,c114,c119)
	dta t(c121,c123,c124,c127,c129)
	dta t(c133,c137,c139)
	dta t(c143)
	dta t(dli2)

	:40 dta t(line+#-1)

	dta t(0)
