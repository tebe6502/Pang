; ---
; ---	FADE
; ---
.local	FADE

	stx	jsr+1
	sty	jsr+2

	ldx	#6
	jsr	wait

	mva	#16	cnt

lcol	mva	<tcol	fcnt
hcol	mva	>tcol	fcnt+1

	ldx	#2
	jsr	wait

jsr	jsr	$ffff

	dec	cnt
	bne	lcol

	ldx	#6

wait	lda:cmp:req cloc
	dex
	bne	wait
	rts

cnt	brk
.end


; ---
; ---	FADE IN
; ---
.local	FADE_IN

	ldx	<fade
	ldy	>fade
	jmp	:fade

fade	ldy	#0
	mva	(fcnt),y	fadr

	iny
	lda	(fcnt),y
	sne:rts

	sta	fadr+1

	lda	(fadr),y
	tax

	iny
	eor	(fcnt),y
	beq	skp

	dey

	pha

	and	#$f0
	beq	plus1

plus16	txa
	add	#$10
	tax

plus1	pla
	and	#$0f
	seq
	inx

	txa
	sta	(fadr),y

skp	adw	fcnt	#3

	jmp	fade
.end


; ---
; ---	FADE OUT
; ---
.local	FADE_OUT

	ldx	<fade
	ldy	>fade
	jmp	:fade

fade	ldy	#0
	mva	(fcnt),y	fadr

	iny
	lda	(fcnt),y
	sne:rts

	sta	fadr+1

	lda	(fadr),y
	tax

	and	#$0f
	beq	skp0

	dex
skp0
	txa
	and	#$f0
	beq	skp1

	txa
	sub	#$10
	tax
skp1
	txa

	sta	(fadr),y

	adw	fcnt	#3

	jmp	fade
.end


; ---
; ---	SAVE COLOR
; ---
.local	SAVE_COLOR

	mva	fade.lcol+1	fcnt
	mva	fade.hcol+1	fcnt+1

loop	ldy	#0
	mva	(fcnt),y	fadr

	iny
	lda	(fcnt),y
	sne:rts

	sta	fadr+1

	lda	(fadr),y

	iny
	sta	(fcnt),y

	dey
	mva	#0	(fadr),y	; black screen (all colors = $00)

	adw	fcnt	#3

	jmp	loop
.end

	.use DLI,NMI
tcol
	dta t(c1,c2,c3,c4,c5,c6,c8,c9)
	dta t(c10,c11,c12,c13,c14,c15,c16,c17,c18,c19)
	dta t(c20,c21,c22,c23,c24,c25,c26,c27,c28,c29)
	dta t(c30,c31,c32,c33,c34,c35,c36,c37,c38,c39)
	dta t(c40,c41,c42,c43)
	dta t(k0,k1)
	dta t(k0_0, k0_1, k0_2, k0_3, k0_4, k0_5, k0_6, k0_7)
	dta t(k1_0, k1_1, k1_2, k1_3, k1_4, k1_5, k1_6, k1_7)
	dta t(k2_0, k2_1, k2_2, k2_3, k2_4, k2_5, k2_6, k2_7)
	dta t(k3_0, k3_1, k3_2, k3_3, k3_4, k3_5, k3_6, k3_7)
	dta t(k4_0, k4_1, k4_2, k4_3, k4_4, k4_5, k4_6, k4_7)
	dta t(k5_0, k5_1, k5_2, k5_3, k5_4, k5_5, k5_6, k5_7)

	dta t(0)
